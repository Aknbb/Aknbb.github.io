<!DOCTYPE html>
<html lang="en">
<head>
    <style>
        #root, html, body {
            margin: 0;
            height: 100%;
        }

        #canvas, #canvas > canvas {
            width: 100%;
            height: 100%;
            display: block;
        }
    </style>
    <title>WebGL Background</title>
</head>
<body>
<div id="root">
    <canvas id="canvas"></canvas>
</div>
<script src="https://webglfundamentals.org/webgl/resources/webgl-utils.js"></script>
<script type="x-shader/x-vertex" id="vertex-shader">
    attribute vec4 a_position;
    void main() {
        gl_Position = a_position;
    }
</script>
<script type="x-shader/x-fragment" id="fragment-shader">
    precision highp float;

    uniform vec2 iResolution;
    uniform float iTime;

    #define NUM_LAYERS 2.
    #define PULSE .005
    #define MAX_PARTICLE_NUMBER 20.
    #define MIN_PARTICLE_NUMBER 1.

    float N21(vec2 p) {
        vec3 a = fract(vec3(p.xyx) * vec3(213.897, 653.453, 253.098));
        a += dot(a, a.yzx + 79.76);
        return fract((a.x + a.y) * a.z);
    }

    vec2 GetPos(vec2 id, vec2 offs, float t) {
        float n = N21(id+offs);
        float n1 = fract(n*10.);
        float n2 = fract(n*100.);
        float a = t+n;
        return offs + vec2(sin(a*n1), cos(a*n2))*.4;
    }

    float df_line(in vec2 a, in vec2 b, in vec2 p)
    {
        vec2 pa = p - a, ba = b - a;
        float h = clamp(dot(pa, ba) / dot(ba, ba), 0., 1.);
        return length(pa - ba * h);
    }

    float line(vec2 a, vec2 b, vec2 uv) {
        float r1 = .04;
        float r2 = .01;

        float d = df_line(a, b, uv);
        float d2 = length(a-b);
        float fade = smoothstep(2., 1., d2);

        fade += smoothstep(.05, .02, abs(d2-.75));
        return smoothstep(r1, r2, d)*fade;
    }

    float NetLayer(vec2 st, float n, float t) {
        vec2 id = floor(st)+n;

        st = fract(st)-.5;

        vec2 p[9];
        int i=0;
        p[0] = GetPos(id, vec2(-1, -1), t);
        p[1] = GetPos(id, vec2(0, -1), t);
        p[2] = GetPos(id, vec2(1, -1), t);
        p[3] = GetPos(id, vec2(-1, 0), t);
        p[4] = GetPos(id, vec2(0, 0), t);
        p[5] = GetPos(id, vec2(1, 0), t);
        p[6] = GetPos(id, vec2(-1, 1), t);
        p[7] = GetPos(id, vec2(0, 1), t);
        p[8] = GetPos(id, vec2(1, 1), t);

        float m = 0.;
        float sparkle = 0.;
        // Patlama kısmı
        for (int i=0; i<9; i++) {
            m += line(p[4], p[i], st);

            float d = length(st-p[i]);

            float s = (.005/(d*d));
            s *= smoothstep(1., .7, d) * PULSE;
            sparkle += s;
        }

        m += line(p[1], p[3], st);
        m += line(p[1], p[5], st);
        m += line(p[7], p[5], st);
        m += line(p[7], p[3], st);

        float sPhase = (sin(t+n)+sin(t*.05))*.25+.5;
        sPhase += pow(sin(t*.1)*.5+.5, 50.)*5.;
        m += sparkle*sPhase;

        return m;
    }

    vec2 rotate(vec2 target, float angle) {
        return target * mat2(cos(angle), -sin(angle), sin(angle), cos(angle));
    }

    void main() {
        vec2 uv = (gl_FragCoord.xy-iResolution.xy/2.)/iResolution.y;

        float time = iTime*.01;

        vec2 st = rotate(uv, time);

        float m = 0.;
        for (float i=0.; i<1.; i+=1./NUM_LAYERS) {
            float z = fract(time+i);
            float size = mix(MAX_PARTICLE_NUMBER, MIN_PARTICLE_NUMBER, z);
            float fade = smoothstep(0., .6, z);

            m += fade * NetLayer(st*size*z*3., i, iTime*.5);
        }

        float glow = -uv.y*0.15;

//        float baseColorMultiplier = iTime*0.5;
//        vec3 baseColor = vec3(sin(baseColorMultiplier*.1), cos(baseColorMultiplier*.1), -sin(baseColorMultiplier*.24))*.4+.6;
        vec3 baseColor = vec3(0.36328125, 0.7421875, 0.90625);
//        vec3 baseColor = vec3(0.22265625, 0.4765625, 0.5859375);
        vec3 col = baseColor*m;
        col += baseColor*glow;

        col *= 1.-dot(uv, uv);
        time = mod(iTime, 230.);
        col *= smoothstep(0., 20., time)*smoothstep(224., 200., time) / 10.;

        gl_FragColor = vec4(col, 1);
    }
</script>
<script src="app.js"></script>
</body>
</html>
